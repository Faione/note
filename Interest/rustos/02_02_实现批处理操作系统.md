## 实现批处理操作系统

操作系统与应用程序放置到同一个可执行文件，应用加载机制: 应用放置采用"静态绑定", 而操作系统加载应用则采"动态加载"的方式
- 静态绑定: 通过一定的编程技巧，把多个应用程序代码和批处理操作系统代码“绑定”在一起
- 动态加载: 基于静态编码留下的“绑定”信息，操作系统可以找到每个应用程序文件二进制代码的起始地址和长度，并能加载到内存中运行

### 链接应用到内核

应用程序通过链接脚本，链接到OS镜像中的数据段，同时除链接应用程序本身外，链接脚本还提供了应用程序的数量、起始位置的关键信息，这使得批处理系统才在运行时对它们进行管理并能够加载到内存

```
_num_app:
    .quad 5
    .quad app_0_start
    .quad app_1_start
    .quad app_2_start
    .quad app_3_start
    .quad app_4_start
    .quad app_4_end
```
批处理系统的核心是AppManager, 其主要功能是
- 保存应用数量和各自的位置信息，以及当前执行的应用编号
- 根据应用程序的位置信息，初始化好应用所需的内存空间，并加载应用运行

AppManager的核心方法是`load_app`，在载入应用时:
1. app_id从0开始编码，首先判断是否还有app需要执行，来进入执行逻辑或退出
2. 执行`fence.i`指令，清空icache中的内容，从而保证CPU访问内存数据和代码的正确性
3. 初始化应用内存空间，即从`0x80400000`开始的一段内存空间
4. 从保存的应用信息中，获取数据段中的，要执行应用的位置，并将其复制到先前初始化好的内存空间，从而完成应用的载入

```rust
unsafe fn load_app(&self, app_id: usize) {
    if app_id >= self.num_app {
        panic!("All applications completed!")
    }
    println!("[kernel] Loading app_{}", app_id);
    // clear instruction cache
    asm!("fence.i");
    // clear app area
    core::slice::from_raw_parts_mut(APP_BASE_ADDRESS as *mut u8, APP_SIZE_LIMIT).fill(0);
    let app_src = core::slice::from_raw_parts(
        self.app_start[app_id] as *const u8,
        self.app_start[app_id + 1] - self.app_start[app_id],
    );
    let app_dst = core::slice::from_raw_parts_mut(APP_BASE_ADDRESS as *mut u8, app_src.len());
    app_dst.copy_from_slice(app_src);
}

```

## 实现特权级切换

应用程序建立在批处理操作系统提供的AEE(Application Execution Environment)中，批处理操作系统为了建立好应用程序的执行环境，需要在执行应用程序之前进行一些初始化工作，并监控应用程序的执行，具体体现在:
- 当启动应用程序的时候，需要初始化应用程序的用户态上下文，并能切换到用户态执行应用程序
- 当应用程序发起系统调用（即发出 Trap）之后，需要到批处理操作系统中进行处理
- 当应用程序执行出错的时候，需要到批处理操作系统中杀死该应用并加载运行下一个应用
- 当应用程序执行结束的时候，需要到批处理操作系统中加载运行下一个应用（实际上也是通过系统调用`sys_exit`来实现的）

S特权级中与Trap相关的**控制状态寄存器**(CSR, Control and Status Register)

| CSR名称 |                        该CSR与Trap相关的功能                         |
| :-----: | :------------------------------------------------------------------: |
| sstatus |      `SPP`等字段给出Trap发生之前CPU处在哪个特权级（S/U）等信息       |
|  sepc   | 当 Trap 是一个异常的时候，记录 Trap 发生之前执行的最后一条指令的地址 |
| scause  |                           描述 Trap 的原因                           |
|  stval  |                          给出 Trap 附加信息                          |
|  stvec  |                     控制 Trap 处理代码的入口地址                     |

当执行一条 Trap 类指令(如 ecall 时)，CPU 发现触发了一个异常并需要进行特殊处理，这涉及到 执行环境切换 。具体而言，用户态执行环境中的应用程序通过 ecall 指令向内核态执行环境中的操作系统请求某项服务功能，那么处理器和操作系统会完成到内核态执行环境的切换，并在操作系统完成服务后，再次切换回用户态执行环境，然后应用程序会紧接着 ecall 指令的后一条指令位置处继续执行

### 硬件功能

当CPU执行完一条指令(如ecall)并准备从U特权级陷入(Trap)到S特权级时, 硬件自动完成如下事情
- `sstatus`的`SPP`字段会被修改为CPU当前的特权级(U/S)
- `sepc`会被修改为Trap处理完成后默认会执行的下一条指令的地址
- `scause/stval`分别会被修改成这次Trap的原因以及相关的附加信息
- CPU 会跳转到`stvec`所设置的Trap处理入口地址，并将当前特权级设置为S，然后从Trap处理入口地址处开始执行

而当CPU完成Trap处理准备返回的时候，需要通过一条S特权级的特权指令`sret`来完成，这一条指令具体完成以下功能
- CPU会将当前的特权级按照`sstatus`的SPP字段设置为U或者S
- CPU会跳转到`sepc`寄存器指向的那条指令，然后继续执行

### 软件功能

#### 用户栈与内核栈

使用两个不同的栈主要是为了安全性：如果两个控制流（即应用程序的控制流和内核的控制流）使用同一个栈，在返回之后应用程序就能读到 Trap 控制流的历史信息，比如内核一些函数的地址，这样会带来安全隐患。于是，我们要做的是，在批处理操作系统中添加一段汇编代码，实现从用户栈切换到内核栈，并在内核栈上保存应用程序控制流的寄存器状态

#### 中断上下文

特权级切换的核心时对于Trap的管理
1. 应用程序通过`ecall`进入到内核状态时，操作系统保存被打断的应用程序的 Trap 上下文
2. 操作系统根据Trap相关的CSR寄存器内容，完成系统调用服务的分发与处理
3. 操作系统完成系统调用服务后，需要恢复被打断的应用程序的Trap上下文，并通`sret`让应用程序继续执行

TrapContext将会被保存在栈上，占用`34*8`byte的空间，其中前32byte为`x0~x31`32个寄存器的值，sstatus与sepc分别为特权级与中断前将要执行的下一条指令，共占用2byte

```rust
pub struct TrapContext {
    pub x: [usize; 32],
    pub sstatus: Sstatus,
    pub sepc: usize,
}
```

**Trap上下文的保存与恢复**

批处理操作系统初始化时，需要修改`stvec`寄存器来指向正确的Trap处理入口点，通过引用外部符号，与risc-v库提供的`stvec::write`函数来实现
Trap 处理的总体流程如下
1. 首先通过`__alltraps`将Trap上下文保存在内核栈上
2. 然后跳转到使用Rust编写的`trap_handler`函数完成 Trap 分发及处理
3. 当`trap_handler`返回之后，使用`__restore`从保存在内核栈上的Trap上下文恢复寄存器

>RISC-V 中读写 CSR 的指令是一类能不会被打断地完成多个读写操作的指令
>RISC-V 架构中常规的数据处理和访存类指令只能操作通用寄存器而不能操作 CSR

```s
# 用于节省开发的宏
.altmacro
.macro SAVE_GP n
    sd x\n, \n*8(sp)
.endm
.macro LOAD_GP n
    ld x\n, \n*8(sp)
.endm
    .section .text
    .globl __alltraps
    .globl __restore
    .align 2 # 使得代码按4字节($2^{2}$)对齐
__alltraps:
    csrrw sp, sscratch, sp # 交换sp与sscratch，交换前，sp指向用户栈，sscratch指向内核栈
    addi sp, sp, -34*8 # 分配34*8字节的栈帧，用来在内核栈上保存Trap上下文
    sd x1, 1*8(sp) # 跳过寄存器x0与tp(x4), 但仍然为其保留位置
    sd x3, 3*8(sp)
    .set n, 5
    .rept 27
        SAVE_GP %n
        .set n, n+1
    .endr
    # t0/t1/t2是x1-x31中某几个通用寄存器的别名，先前以及保存到栈上
    csrr t0, sstatus # 将csr寄存器sstatus中的内容保存到寄存器t0中
    csrr t1, sepc
    csrr t2, sscratch
    sd t0, 32*8(sp) # 将寄存器的内容转移到栈上
    sd t1, 33*8(sp)
    sd t2, 2*8(sp)
    mv a0, sp # 让寄存器a0指向内核栈的栈指针，也即刚刚保存的TrapContext的地址，作为trap_handler的传入参数
    call trap_handler
__restore:
    ...
```

trap_handler为使用rust编写的函数，当其返回之后，便会从调用trap_handler的下一条指令开始执行，即`__restore`(使用函数签名与goto理解)

```s
__restore:
    mv sp, a0
    ld t0, 32*8(sp)
    ld t1, 33*8(sp)
    ld t2, 2*8(sp)
    csrw sstatus, t0
    csrw sepc, t1
    csrw sscratch, t2
    ld x1, 1*8(sp)
    ld x3, 3*8(sp)
    .set n, 5
    .rept 27
        LOAD_GP %n
        .set n, n+1
    .endr
    addi sp, sp, 34*8
    csrrw sp, sscratch, sp
    sret
```